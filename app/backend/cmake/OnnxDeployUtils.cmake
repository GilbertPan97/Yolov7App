macro(FIND_DEP_PATH LIBRARY_NAME LIBRARY_VERSION OUTPUT_VARIABLE)
    if(NOT THIRD_PARTY)
        message(FATAL_ERROR "THIRD_PARTY dir is not set.")
    endif()

    if(UNIX)
        set(PLATFORM "linux")
        message(STATUS "${LIBRARY_NAME} running on Linux.")
    elseif(WIN32)
        set(PLATFORM "win")
        message(STATUS "${LIBRARY_NAME} running on Windows.")
    endif()

    if(CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64" OR CMAKE_SYSTEM_PROCESSOR STREQUAL "AMD64")
        set(ARCHITECTURE "x64")
        message(STATUS "${LIBRARY_NAME} running on x64 architecture.")
    elseif(CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64")
        set(ARCHITECTURE "aarch64")
        message(STATUS "${LIBRARY_NAME} running on aarch64 architecture.")
    else()
        message(WARNING "${LIBRARY_NAME} running on an unknown architecture.")
    endif()

    if(WITH_GPU)
        set(GPU_CONFIG "-gpu")
        add_definitions(-DWITH_GPU=1)
    else()
        set(GPU_CONFIG "")
        add_definitions(-DWITH_GPU=0)
    endif()

    set(LIBRARY_FULL_NAME "${LIBRARY_NAME}-${PLATFORM}-${ARCHITECTURE}${GPU_CONFIG}-${LIBRARY_VERSION}")

    set(${OUTPUT_VARIABLE} "${THIRD_PARTY}/${LIBRARY_FULL_NAME}")
endmacro()
